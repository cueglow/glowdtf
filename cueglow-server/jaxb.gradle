//adapted from https://stackoverflow.com/a/61621855

sourceSets {
    generated {
        java.srcDir "$generatedDir"
    }
}

// configuration "xjc" holds classpath for running the JAXB XJC compiler
configurations {
    xjc
}

dependencies {
    implementation sourceSets.generated.output

    // dependencies for our code
    implementation 'org.glassfish.jaxb:jaxb-runtime:3.0.2'

    // dependencies for compiling the generated code
    generatedImplementation "org.glassfish.jaxb:jaxb-runtime:3.0.2"

    // dependencies for running xjc
    xjc platform("org.glassfish.jaxb:jaxb-bom:3.0.2")
    xjc "org.glassfish.jaxb:jaxb-xjc"
    xjc "org.glassfish.jaxb:jaxb-runtime"
}

def addXjcTask(taskName, schema, pkg, dest, binding) {
    // If you haven't already, create the generated output dir before running XJC or it will fail
    file(dest).mkdirs()

    // The main XJC task, calls XJCFacade which is the entry point of the XJC JAR
    tasks.create(name: taskName, type: JavaExec) {
        inputs.files schema, binding
        outputs.dir dest
        classpath configurations.xjc
        main 'com.sun.tools.xjc.XJCFacade'
        // To explore available args, download the XJC JAR and run java -jar jaxb-xjc.jar --help
        args schema, "-p", pkg, "-d", dest, "-b", binding
    }
    // Add a dependency on the new task so it gets invoked
    compileGeneratedJava.dependsOn tasks.getByName(taskName)
    compileGeneratedKotlin.dependsOn tasks.getByName(taskName)
}

addXjcTask("generateGdtf", "src/main/resources/gdtf/gdtf.xsd", "org.cueglow.gdtf", "$generatedDir", "src/main/resources/gdtf/gdtf-binding.xjb")

// add task to show source sets for debug
task sourceSets {
    doLast {
        sourceSets.each {
            println(it)
            it.allSource.each {
                println(it)
            }
        }
    }
}
